(DEFINE-FILE-INFO PACKAGE (DEFPACKAGE "INSPHEX" (USE "LISP" "XCL")) READTABLE "XCL" BASE 10)

(IL:FILECREATED "17-Jun-2024 11:35:43" IL:|{DSK}<home>medley>il>utils>INSPHEX.;41| 11281  

      :EDIT-BY "PA"

      :CHANGES-TO (IL:FUNCTIONS FB-HEXDUMP)

      :PREVIOUS-DATE "17-Jun-2024 10:44:53" IL:|{DSK}<home>medley>il>utils>INSPHEX.;40|)


(IL:PRETTYCOMPRINT IL:INSPHEXCOMS)

(IL:RPAQQ IL:INSPHEXCOMS
          ((FILE-ENVIRONMENTS IL:INSPHEX)
           (IL:P (DEFPACKAGE "INSPHEX" (:USE "LISP" "XCL")
                        (:NICKNAMES "HX")
                        (:EXPORT HEXDUMP)))
           (IL:FUNCTIONS CLEAR-HEX-WINDOW CODE-TO-ASCII CREATE-HEX-WINDOW DEACTIVATE-HEX-WINDOW 
                  FB-HEXDUMP HANDLE-MENU HEXDUMP NEXT-HEX-PAGE PRINT-HEX-BLOCK PRINT-HEXDUMP 
                  QUIT-HEX-WINDOW RESET-HEX-WINDOW)
           (IL:STRUCTURES MENU)
           (IL:COMMANDS HD)
           (IL:APPENDVARS (IL:FB.MENU.ITEMS (IL:|Hexdump| FB-HEXDUMP 
                                                   "Shows the hex dump of the selected files.")))))

(DEFINE-FILE-ENVIRONMENT IL:INSPHEX :PACKAGE (DEFPACKAGE "INSPHEX" (:USE "LISP" "XCL"))
   :READTABLE "XCL"
   :COMPILER :COMPILE-FILE)

(DEFPACKAGE "INSPHEX" (:USE "LISP" "XCL")
       (:NICKNAMES "HX")
       (:EXPORT HEXDUMP))

(DEFUN CLEAR-HEX-WINDOW (WINDOW)                       (IL:* IL:\; "Edited 13-May-2024 09:05 by PA")
   "Delete all the text of hex dump WINDOW."
   (LET ((STREAM (IL:WINDOWPROP WINDOW 'OUTSTREAM)))
        (IL:TEDIT.DELETE STREAM 1 (IL:TEDIT.NCHARS STREAM))))

(DEFUN CODE-TO-ASCII (CODE)                            (IL:* IL:\; "Edited  9-Mar-2024 11:10 by PA")
   "Convert a byte code to the corresponding ASCII character."
   (IF (OR (< CODE 32)
           (> CODE 126))
       #\.
       (CODE-CHAR CODE)))

(DEFUN CREATE-HEX-WINDOW (FILE)                        (IL:* IL:\; "Edited  2-Jun-2024 09:44 by PA")
                                                       (IL:* IL:\; "Edited 20-May-2024 08:37 by PA")
                                                       (IL:* IL:\; "Edited 18-May-2024 09:40 by PA")
                                                       (IL:* IL:\; "Edited 12-May-2024 06:04 by PA")
   "Create and return a window to display the hex dump of FILE."
   (LET* ((IN (OPEN FILE :DIRECTION :INPUT :ELEMENT-TYPE '(UNSIGNED-BYTE 8)))
          (COMMANDS (IL:MENUWINDOW (MAKE-MENU :ITEMS '(("Next" :NEXT "Show the next page.")
                                                       ("Reread" :REREAD "Reread the input file.")
                                                       ("Exit" :EXIT "Quit the program."))
                                          :MENUFONT
                                          '(IL:MODERN 12)
                                          :TITLE "Commands" :CENTERFLG T :WHENSELECTEDFN 
                                          #'HANDLE-MENU)))
          (OUT (IL:OPENTEXTSTREAM))
          (TEDIT-PROC (IL:TEDIT OUT NIL NIL '(IL:QUITFN #'QUIT-HEX-WINDOW)))
          (WINDOW (IL:WFROMDS OUT)))
         (IL:ATTACHWINDOW COMMANDS WINDOW 'IL:TOP 'IL:LEFT)
         (IL:WINDOWPROP WINDOW 'INSTREAM IN)
         (IL:WINDOWPROP WINDOW 'OUTSTREAM OUT)
         (IL:WINDOWPROP WINDOW 'BLOCK-OFFSET 0)
         (IL:WINDOWPROP WINDOW 'IL:TITLE (FORMAT NIL "Insphex ~A" FILE))
         (IL:WINDOWADDPROP WINDOW 'IL:CLOSEFN #'DEACTIVATE-HEX-WINDOW)
         (NEXT-HEX-PAGE WINDOW)
         WINDOW))

(DEFUN DEACTIVATE-HEX-WINDOW (WINDOW)                  (IL:* IL:\; "Edited 20-May-2024 08:27 by PA")
   "Clean up allocated resources in WINWOW."
   (LET ((INSTREAM (IL:WINDOWPROP WINDOW 'INSTREAM)))
        (WHEN (AND (STREAMP INSTREAM)
                   (OPEN-STREAM-P INSTREAM))
              (CLOSE INSTREAM))
        (IL:WINDOWPROP WINDOW 'INSTREAM NIL)
        (IL:WINDOWPROP WINDOW 'BLOCK-OFFSET 0)))

(DEFUN FB-HEXDUMP (BROWSER KEY ITEM MENU ARG)          (IL:* IL:\; "Edited 17-Jun-2024 11:34 by PA")
                                                       (IL:* IL:\; "Edited 16-Jun-2024 10:02 by PA")
   (DECLARE (IGNORE MENU ARG))
   "Show the hex dump of the files selected in the File Browser."
   (LET* ((SELECTED (IL:FB.SELECTEDFILES BROWSER))
          (FILES (IF (LISTP SELECTED)
                     SELECTED
                     (LIST SELECTED))))
         (IL:FB.ALLOW.ABORT BROWSER)
         (WHEN (AND (EQL KEY 'IL:LEFT)
                    (STRING-EQUAL (SYMBOL-NAME (FIRST ITEM))
                           (SYMBOL-NAME '|Hexdump|)))
             (DOLIST (FILE FILES)
                 (HEXDUMP (IL:FB.FETCHFILENAME FILE)
                        T)))))

(DEFUN HANDLE-MENU (ITEM MENU KEY)                     (IL:* IL:\; "Edited 20-May-2024 08:36 by PA")
   "Handle hex window menu selection."
   (LET* ((MENU-WINDOW (IL:WFROMMENU MENU))
          (HEX-WINDOW (IL:MAINWINDOW MENU-WINDOW))
          (COMMAND (SECOND ITEM)))
         (IF (MEMBER KEY '(IL:MIDDLE IL:RIGHT))
             NIL
             (ECASE COMMAND
                 (:NEXT (NEXT-HEX-PAGE HEX-WINDOW))
                 (:REREAD 
                    (RESET-HEX-WINDOW HEX-WINDOW)
                    (NEXT-HEX-PAGE HEX-WINDOW))
                 (:EXIT (QUIT-HEX-WINDOW HEX-WINDOW))))))

(DEFUN HEXDUMP (FILE &OPTIONAL NEWIN-P)                (IL:* IL:\; "Edited 11-May-2024 09:34 by PA")
                                                       (IL:* IL:\; "Edited 10-May-2024 04:41 by PA")
                                                       (IL:* IL:\; "Edited  6-May-2024 12:44 by PA")
                                                       (IL:* IL:\; "Edited  3-May-2024 10:23 by PA")
                                                       (IL:* IL:\; "Edited 30-Apr-2024 12:24 by PA")
                                                       (IL:* IL:\; "Edited  5-Mar-2024 09:13 by PA")
   "Show the contents of FILE in hex and ASCII."
   (COND
      ((NOT (PROBE-FILE FILE))
       (FORMAT *ERROR-OUTPUT* "~&File ~A not found.~%" FILE))
      (NEWIN-P (CREATE-HEX-WINDOW FILE))
      (T (PRINT-HEXDUMP FILE))))

(DEFUN NEXT-HEX-PAGE (WINDOW)                          (IL:* IL:\; "Edited 31-May-2024 10:34 by PA")
                                                       (IL:* IL:\; "Edited 20-May-2024 08:07 by PA")
   "Display in WINDOW the next page of the hex dump."
   (LET* ((INSTREAM (IL:WINDOWPROP WINDOW 'INSTREAM))
          (OUTSTREAM (IL:WINDOWPROP WINDOW 'OUTSTREAM))
          MORE-BYTES-P PAGE)
         (SETF PAGE (WITH-OUTPUT-TO-STRING (STREAM)
                           (DOTIMES (I 32)
                               (SETF MORE-BYTES-P (PRINT-HEX-BLOCK INSTREAM (IL:WINDOWPROP
                                                                             WINDOW
                                                                             'BLOCK-OFFSET)
                                                         STREAM))
                               (IL:WINDOWPROP WINDOW 'BLOCK-OFFSET (+ (IL:WINDOWPROP WINDOW
                                                                             'BLOCK-OFFSET)
                                                                      16))
                               (UNLESS MORE-BYTES-P (RETURN)))))
         (UNLESS (STRING= PAGE "")
             (IL:TEXTPROP OUTSTREAM 'IL:READONLY NIL)
             (CLEAR-HEX-WINDOW WINDOW)
             (IL:TEDIT.INSERT OUTSTREAM PAGE)
             (IL:TEDIT.STREAMCHANGEDP OUTSTREAM T)
             (IL:TEDIT.PROMPTPRINT OUTSTREAM "" T)
             (IL:TEXTPROP OUTSTREAM 'IL:READONLY T))))

(DEFUN PRINT-HEX-BLOCK (IN OFFSET &OPTIONAL (OUT *STANDARD-OUTPUT*))
                                                       (IL:* IL:\; "Edited 20-May-2024 10:21 by PA")
                                                       (IL:* IL:\; "Edited  8-May-2024 12:29 by PA")
                                                       (IL:* IL:\; "Edited  4-May-2024 08:59 by PA")
                                                       (IL:* IL:\; "Edited  2-May-2024 09:24 by PA")
                                                       (IL:* IL:\; "Edited  1-May-2024 08:42 by PA")
   "Read a block of bytes from IN at OFFSET and print them in hex to OUT.
Returns NIL if no more bytes are available for reading, the last read byte otherwise."
   (LET (BYTE BLOCK)
        (DOTIMES (I 16)
            (SETF BYTE (READ-BYTE IN NIL))
            (WHEN (AND BYTE (ZEROP I))
                  (FORMAT OUT "~&~8,'0X  " OFFSET))
            (COND
               (BYTE (PUSH BYTE BLOCK)
                     (FORMAT OUT "~2,'0X~A" BYTE (IF (= I 7)
                                                     "  "
                                                     " ")))
               (T (RETURN))))
        (SETF BLOCK (NREVERSE BLOCK))
        (WHEN BLOCK
            (FORMAT OUT "~60T~{~C~}~%" (MAPCAR #'CODE-TO-ASCII BLOCK)))
        BYTE))

(DEFUN PRINT-HEXDUMP (FILE)                            (IL:* IL:\; "Edited 20-May-2024 10:43 by PA")
                                                       (IL:* IL:\; "Edited 10-May-2024 04:40 by PA")
   "Print to standard output the contents of FILE in hex and ASCII."
   (WITH-OPEN-FILE (IN FILE :DIRECTION :INPUT :ELEMENT-TYPE '(UNSIGNED-BYTE 8)
                       :IF-DOES-NOT-EXIST NIL)
          (DO* ((OFFSET 0 (+ OFFSET 16))
                (MORE-BYTES-P (PRINT-HEX-BLOCK IN OFFSET)
                       (PRINT-HEX-BLOCK IN OFFSET)))
               ((NOT MORE-BYTES-P)
                T))))

(DEFUN QUIT-HEX-WINDOW (WINDOW &OPTIONAL TEXTSTREAM TEXTOBJ DUMMY)
                                                       (IL:* IL:\; "Edited  2-Jun-2024 09:50 by PA")
                                                       (IL:* IL:\; "Edited 20-May-2024 08:28 by PA")
                                                       (IL:* IL:\; "Edited 13-May-2024 08:54 by PA")
   (DECLARE (IGNORE TEXTSTREAM TEXTOBJ DUMMY))
   "Close and terminate hex dump WINDOW."
   (DEACTIVATE-HEX-WINDOW WINDOW)
   (IL:TEDIT.QUIT (IL:WINDOWPROP WINDOW 'OUTSTREAM)))

(DEFUN RESET-HEX-WINDOW (WINDOW)                       (IL:* IL:\; "Edited 20-May-2024 08:31 by PA")
                                                       (IL:* IL:\; "Edited 12-May-2024 06:11 by PA")
   "Reinitialize program state for rereading input file."
   (LET ((INSTREAM (IL:WINDOWPROP WINDOW 'INSTREAM)))
        (WHEN (AND (STREAMP INSTREAM)
                   (OPEN-STREAM-P INSTREAM))
              (FILE-POSITION INSTREAM 0))
        (IL:WINDOWPROP WINDOW 'BLOCK-OFFSET 0)))

(DEFINE-RECORD MENU IL:MENU)

(DEFCOMMAND (HD :EVAL) (XCL-USER::FILE &OPTIONAL (XCL-USER::NEWIN-P NIL)) (HEXDUMP XCL-USER::FILE 
                                                                                 XCL-USER::NEWIN-P))

(IL:APPENDTOVAR IL:FB.MENU.ITEMS (IL:|Hexdump| FB-HEXDUMP "Shows the hex dump of the selected files."
                                        ))
(IL:DECLARE\: IL:DONTCOPY
  (IL:FILEMAP (NIL (1314 1586 (CLEAR-HEX-WINDOW 1314 . 1586)) (1588 1848 (CODE-TO-ASCII 1588 . 1848)) (
1850 3521 (CREATE-HEX-WINDOW 1850 . 3521)) (3523 3943 (DEACTIVATE-HEX-WINDOW 3523 . 3943)) (3945 4739 
(FB-HEXDUMP 3945 . 4739)) (4741 5366 (HANDLE-MENU 4741 . 5366)) (5368 6258 (HEXDUMP 5368 . 6258)) (
6260 7765 (NEXT-HEX-PAGE 6260 . 7765)) (7767 9135 (PRINT-HEX-BLOCK 7767 . 9135)) (9137 9770 (
PRINT-HEXDUMP 9137 . 9770)) (9772 10354 (QUIT-HEX-WINDOW 9772 . 10354)) (10356 10863 (RESET-HEX-WINDOW
 10356 . 10863)))))
IL:STOP
